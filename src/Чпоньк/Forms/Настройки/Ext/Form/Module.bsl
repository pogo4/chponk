#Область ПрограммныйИнтерфейс

&НаКлиенте
Асинх Функция Настройки() Экспорт
	
	ЗаполнитьОбъектИзОбъектаВладельца(ЭтотОбъект.ВладелецФормы.Объект);
	ПрочитатьНастройки();
	
	Если Объект.Настройки.Количество() = 0 Тогда
		Ждать ИнициализироватьНастройки();
	КонецЕсли;
	
	Возврат СериализованныеНастройки();
	
КонецФункции

&НаКлиенте
Процедура Записать() Экспорт
	
	ЗаполнитьОбъектИзОбъектаВладельца(ЭтотОбъект.ВладелецФормы.Объект);
	СохранитьНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьОбъектИзОбъектаВладельца(ЭтотОбъект.ВладелецФормы.Объект);
	
	Если Объект.Настройки.Количество() = 0 Тогда
		ПрочитатьНастройки();
	КонецЕсли;
	
	СоздатьОбновитьЭлементыНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаПриИзменении()
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройки

&НаКлиенте
Асинх Процедура НачалоВыбораФайлаВНастройки(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ТекущийФайл = Новый Файл(Элемент.ТекстРедактирования);
	Диалог.Каталог = ТекущийФайл.Путь;
	Диалог.МножественныйВыбор = Ложь;
	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("Ключ", Элемент.Имя);
	НайденныеСтроки = Объект.Настройки.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		НайденныеСтроки[0].Значение = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура НачалоВыбораДиректорииВНастройки(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ТекущийФайл = Новый Файл(Элемент.ТекстРедактирования);
	Диалог.Каталог = ТекущийФайл.Путь;
	Диалог.МножественныйВыбор = Ложь;
	ВыбранныеФайлы = Ждать Диалог.ВыбратьАсинх();
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("Ключ", Элемент.Имя);
	НайденныеСтроки = Объект.Настройки.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		НайденныеСтроки[0].Значение = ВыбранныеФайлы[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ПоУмолчанию(Команда)
	
	Объект.Настройки.Очистить();
	Ждать ИнициализироватьНастройки();
	СоздатьОбновитьЭлементыНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	СохранитьНастройки();
	Закрыть(СериализованныеНастройки());
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНастройки();
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СериализованныеНастройки()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Возврат Обработка.ТаблицаВМассивСтруктур(Объект.Настройки.Выгрузить());
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОбъектИзОбъектаВладельца(Знач ОбъектВладельца)
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ЗаполнитьОбъектФормы(Объект, ОбъектВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройки()
	
	СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить("Чпоньк", "НастройкиПрограммы");
	
	Если ТипЗнч(СохраненныеНастройки) = Тип("ТаблицаЗначений") Тогда
		Объект.Настройки.Загрузить(СохраненныеНастройки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	ХранилищеОбщихНастроек.Сохранить(
		"Чпоньк",
		"НастройкиПрограммы",
		Объект.Настройки.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Асинх Функция ИнициализироватьНастройки() Экспорт
	
	КаталогВременныхФайлов = Ждать КаталогВременныхФайловАсинх();
	КаталогВыгрузки = КаталогВременныхФайлов + "Чпоньк";
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияПриложения = СистемнаяИнформация.ВерсияПриложения;
	
	ТипыПлатформыWindows = Новый Массив;
	ТипыПлатформыWindows.Добавить(ТипПлатформы.Windows_x86);
	ТипыПлатформыWindows.Добавить(ТипПлатформы.Windows_x86_64);
	
	ЭтоWindows = ТипыПлатформыWindows.Найти(СистемнаяИнформация.ТипПлатформы) <> Неопределено;
	
	РазделительДиректорий = "/";
	РасширениеИсполняемогоФайла = "";
	
	Если ЭтоWindows Тогда
		
		РазделительДиректорий = "\";
		РасширениеИсполняемогоФайла = ".exe";
		
	КонецЕсли;
	
	КаталогПрограммы = КаталогПрограммы();
	ИсполняемыйФайл1с = СтрШаблон("%1%2%3", КаталогПрограммы, "1cv8", РасширениеИсполняемогоФайла);
	ВременнаяИнформационнаяБаза = СтрШаблон("%1%2tempdb", КаталогВыгрузки, РазделительДиректорий);
	ФайлРасширения = СтрШаблон("%1%2Чпоньк.cfe", КаталогВыгрузки, РазделительДиректорий);
	ИсходныеФайлыРасширения = СтрШаблон("%1%2src", КаталогВыгрузки, РазделительДиректорий);
	ФайлВерсий = СтрШаблон("%1%2ConfigDumpInfo.xml", КаталогВыгрузки, РазделительДиректорий);
	КаталогДокументов = Ждать КаталогДокументовАсинх();
	РабочийКаталог = СтрШаблон("%1Чпоньк", КаталогДокументов);
	ИмяИсполняемогоФайлаIbcmd = СтрШаблон("%1ibcmd%2", КаталогПрограммы, РасширениеИсполняемогоФайла);
	ИсполняемыйФайлIbcmd = Новый Файл(ИмяИсполняемогоФайлаIbcmd);
	УтилитаIbcmdУстановлена = Ждать ИсполняемыйФайлIbcmd.СуществуетАсинх();
	
	ДобавитьНастройку("ВерсияПриложения", ВерсияПриложения);
	ДобавитьНастройку("РазделительДиректорий", РазделительДиректорий);
	ДобавитьНастройку("ИсполняемыйФайл1с", ИсполняемыйФайл1с, "Файл");
	ДобавитьНастройку("УтилитаIbcmdУстановлена", УтилитаIbcmdУстановлена);
	ДобавитьНастройку("ИнформационнаяБазаДляСборкиРасширения", ВременнаяИнформационнаяБаза, "Директория");
	ДобавитьНастройку("ФайлРасширения", ФайлРасширения, "Файл");
	ДобавитьНастройку("ИсходныеФайлыРасширения", ИсходныеФайлыРасширения, "Директория");
	ДобавитьНастройку("ФайлВерсий", ФайлВерсий, "Директория");
	ДобавитьНастройку("РабочийКаталог", РабочийКаталог, "Директория");
	ДобавитьНастройку("Интервал", 1);
	ДобавитьНастройку("ВключаяПодкаталоги", Ложь);
	ДобавитьНастройку("Фильтр", "*.bsl");
	ДобавитьНастройку("СтрокаСоединения", СтрокаСоединенияИнформационнойБазы());
	ДобавитьНастройку("Пользователь", ИмяПользователя());
	ДобавитьНастройку("Пароль", "", "Пароль");
	ДобавитьНастройку("ИспользоватьCom", Истина);
	ДобавитьНастройку("HTTPСервис", "");
	
	Если УтилитаIbcmdУстановлена Тогда
		ДобавитьНастройку("ИсполняемыйФайлIbcmd", ИмяИсполняемогоФайлаIbcmd, "Файл");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьНастройку(Ключ, Значение, Тип = Неопределено)
	
	Настройка = Объект.Настройки.Добавить();
	Настройка.Ключ = Ключ;
	Настройка.Значение = Значение;
	Настройка.Тип = Тип;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьОбновитьЭлементыНастроек()
	
	КоличествоЭлементовНастроек = Элементы.ГруппаНастройки.ПодчиненныеЭлементы.Количество();
	
	Для Сч = 1 По КоличествоЭлементовНастроек Цикл
		
		Элемент = Элементы.ГруппаНастройки.ПодчиненныеЭлементы[КоличествоЭлементовНастроек - Сч];
		Элементы.Удалить(Элемент);
		
	КонецЦикла;
	
	Для Каждого Настройка Из Объект.Настройки Цикл
		
		ЭлементНастройки = Элементы.Добавить(Настройка.Ключ, Тип("ПолеФормы"), Элементы.ГруппаНастройки);
		ИндексНастройки = Объект.Настройки.Индекс(Настройка);
		ТипЗначения = ТипЗнч(Объект.Настройки[ИндексНастройки].Значение);
		
		Если ТипЗначения = Тип("Булево") Тогда
			ЭлементНастройки.Вид = ВидПоляФормы.ПолеФлажка;
		Иначе
			ЭлементНастройки.Вид = ВидПоляФормы.ПолеВвода;
		КонецЕсли;
		
		ЭлементНастройки.Заголовок = Настройка.Ключ;
		ЭлементНастройки.ПутьКДанным = СтрШаблон("Объект.Настройки[%1].Значение", ИндексНастройки);
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗначения);
		
		ОписаниеТипа = Новый ОписаниеТипов(МассивТипов);
		ЭлементНастройки.ОграничениеТипа = ОписаниеТипа;
		
		Если Настройка.Тип = "Директория" Тогда
			
			ЭлементНастройки.КнопкаВыбора = Истина;
			ЭлементНастройки.УстановитьДействие("НачалоВыбора", "НачалоВыбораДиректорииВНастройки");
			
		КонецЕсли;
		
		Если Настройка.Тип = "Файл" Тогда
			
			ЭлементНастройки.КнопкаВыбора = Истина;
			ЭлементНастройки.УстановитьДействие("НачалоВыбора", "НачалоВыбораФайлаВНастройки");
			
		КонецЕсли;
		
		Если Настройка.Тип = "Пароль" Тогда
			
			ЭлементНастройки.РежимПароля = Истина;
			
		КонецЕсли;
		
		ЭлементНастройки.УстановитьДействие("ПриИзменении", "НастройкаПриИзменении");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти