
&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.Значения.МножественныйВыбор Тогда
		
		ПараметрыОтбора = Новый Структура("Выбрано", Истина);
		ВыбранныеСтроки = Значения.НайтиСтроки(ПараметрыОтбора);
		
		РезультатВыбора = Новый Массив;
		
		Для Каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
			РезультатВыбора.Добавить(ВыбраннаяСтрока.Значение);
		КонецЦикла;
		
	Иначе
		
		ТекущиеДанные = Элементы.Значения.ТекущиеДанные;
		РезультатВыбора = Неопределено;
		
		Если ТекущиеДанные <> Неопределено Тогда
			РезультатВыбора = ТекущиеДанные.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("СписокЗначений") 
		Или Не ЗначениеЗаполнено(Параметры.СписокЗначений) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из Параметры.СписокЗначений Цикл
		ЗаполнитьЗначенияСвойств(Значения.Добавить(), ЭлементСписка);
	КонецЦикла;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.Свойство("МножественныйВыбор") Тогда
		Элементы.Значения.МножественныйВыбор = Параметры.МножественныйВыбор;
	КонецЕсли;
	
	Если Элементы.Значения.МножественныйВыбор Тогда
		
		Элементы.Значения.РежимВыделения = РежимВыделенияТаблицы.Множественный;
		Элементы.ЗначенияВыбрано.Видимость = Истина;
		
	Иначе
		
		Элементы.Значения.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
		Элементы.ЗначенияВыбрано.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.Значения.ВысотаВСтрокахТаблицы = Значения.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Значения.НайтиПоИдентификатору(ВыбраннаяСтрока).Значение);
	
КонецПроцедуры
